Properties ver = new Properties()
File versionFile = new File('version.txt')

// build is to distinguish bugfix releases, as the fourth number in the mod version. Resets when any of the others increase.
ver.build = 0

// revision is used by liteloader, and is required to increase (by an arbitrary amount) with each public release
ver.revision = 0

// with the default values set above, now try reading the actual values
ver.load(versionFile.newDataInputStream())

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
          url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

sourceCompatibility = targetCompatibility = '1.7'
compileJava {
    sourceCompatibility = targetCompatibility = '1.7'
}

String minecraft_versions = '1.8.9'
version = '1.7.1.mc18-pre-' + ver.build
group = 'uristqwerty'
archivesBaseName = 'CraftGuide'

ext.llrevision = '03'
project(':litemod') {
    apply plugin: 'net.minecraftforge.gradle.liteloader'
    version = '1.7.1.mc18-pre-' + ver.build
    group = 'uristqwerty'
    archivesBaseName = 'CraftGuide'
    minecraft {
        version = '1.8.9'
        runDir = 'run'
        mappings = 'stable_22'
    }

    jar {
        exclude '**/CraftGuide_FML.*'
        exclude 'mcmod.info'
    }
}
sourceSets {
    main {
        java {
            exclude '**/LiteMod_CraftGuide.*'
        }
    }
}

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'

    replace '@MOD_VERSION@', project.version
    replaceIn 'CraftGuide_FML.java'
    replaceIn 'LiteMod_CraftGuide.java'
}

repositories {
    maven {
        name = 'Player\'s Repo'
        url = 'http://maven.ic2.player.to/'
    }
    ivy {
        name 'BuildCraft'
        artifactPattern 'http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]'
    }
}

dependencies {
    deobfCompile name: 'buildcraft', version: '7.2.8', classifier: 'api'
    compile 'net.industrial-craft:industrialcraft-2:2.5.8-ex19:dev'
}

task themeZip(type: Zip) {
    from 'themes'
    archiveName 'CraftGuideResources.zip'
    destinationDir sourceSets.main.output.resourcesDir
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version
    inputs.property 'revision', ver.revision

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'litemod.json'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'llrevision':project.llrevision, 'revision':ver.revision
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'litemod.json'
    }
}

processResources.dependsOn themeZip

jar {
    exclude '**/LiteMod_CraftGuide.*'
    exclude 'litemod.json'
    classifier = 'forge'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from (sourceSets.main.output) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    from (sourceSets.main.allJava) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task publish(type: Exec) {
    commandLine 'cmd', '/c', 'publish.bat', project.version, minecraft_versions
}

curseforge {
    apiKey = project.curseForgeApiKey
    project {
        id = '75557'
        releaseType = 'alpha' //alpha|beta|release
        changelog = file('changes.txt')

        addGameVersion minecraft_versions
        mainArtifact jar
        addArtifact deobfJar
        addArtifact apiJar
        addArtifact sourcesJar
    }
}

publish.dependsOn build
//curseforge.dependsOn publish

artifacts {
    archives deobfJar
    archives apiJar
    archives sourcesJar
}

build.doLast {
    ver.build = '' + ((ver.build as int) + 1)
    ver.revision = '' + ((ver.revision as int) + 1)
    ver.store(versionFile.newWriter(), null)
}

