buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'liteloader'
apply plugin: 'curseforge'

version = "1.6.8.3"
group = "uristqwerty" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "CraftGuide"

ext.llrevision = "03"

minecraft {
    version = "1.7.10-10.13.0.1180"
    runDir = "run"

    replaceIn "CraftGuide_FML.java"
    replaceIn "LiteMod_CraftGuide.java"
    replace "@MOD_VERSION@", project.version
}

repositories {
    maven {
        name = "Player's Repo"
        url = "http://maven.ic2.player.to/"
    }
}

dependencies {
    compile 'com.mod-buildcraft:buildcraft:6.0.17:dev'
    compile 'net.industrial-craft:industrialcraft-2:2.2.760-experimental:dev'
}

task themeZip(type: Zip) {
    from 'themes'
    archiveName 'CraftGuideResources.zip'
    destinationDir sourceSets.main.output.resourcesDir
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'litemod.json'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'llrevision':project.llrevision
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'litemod.json'
    }
}

processResources.dependsOn themeZip

jar {
    exclude '**/LiteMod_CraftGuide.*'
    exclude 'litemod.json'
}

jarLiteloader {
    exclude '**/CraftGuide_FML.*'
    exclude 'mcmod.info'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from (sourceSets.main.output) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    from (sourceSets.main.allJava) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

curse {
    projectId = '75557'
    apiKey = project.curseForgeApiKey
    releaseType = 'release'

    addGameVersion '1.7.10'
    additionalArtifact deobfJar, apiJar, sourcesJar

    changelog '''\
More translations (thanks to everyone who has contributed!)
A variety of long-needed bugfixes:
  - fixed NPE if keybind disabled
  - fixed tooltips for multi-item filters
  - don't crash the game when encountering an ItemStack with a null item
Features added since 1.6.8.1:
  - in-game theme switch button
  - right-clicking item search text input clears it
  - item list finally handles NBT properly
  - workaround for opening slowly with some AE2 versions
'''

}

curse.dependsOn build

artifacts {
    archives deobfJar
    archives apiJar
    archives sourcesJar
}